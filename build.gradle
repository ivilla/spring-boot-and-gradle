buildscript {
    ext {
        springBootVersion = '1.3.0.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

apply plugin: 'java'
apply plugin: 'spring-boot'
apply plugin: 'war'
apply plugin: 'io.spring.dependency-management'

war {
    baseName = 'villa-qa'
    version = '0.1-SNAPSHOT'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
    maven {
        url "http://oss.jfrog.org/artifactory/oss-release-local"
    }
}


dependencies {
    [
      "org.springframework.boot:spring-boot-starter-web",
      "org.springframework.boot:spring-boot-starter-data-rest",
      "org.springframework.boot:spring-boot-starter-data-jpa",
      "mysql:mysql-connector-java:5.1.6"
    ].each { dep ->
        compile(dep) {
          exclude module: "spring-boot-starter-logging"
          exclude module: "logback-classic"
        }
    }
    compile(
      "org.springframework.boot:spring-boot-starter-log4j2",
      "com.fasterxml.jackson.core:jackson-core",
      "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml"
    )
    providedRuntime ("org.springframework.boot:spring-boot-starter-tomcat")
    testCompile (
      "org.springframework.boot:spring-boot-starter-test",
      "org.spockframework:spock-core:0.7-groovy-2.0",
      "junit:junit:4.12"
    )

}

task wrapper(type: Wrapper) {
    gradleVersion = '2.7'
}

test {
  testLogging.showStandardStreams = true
  testLogging {
  // Show that tests are run in the command-line output
    events 'started', 'passed'
  }
}




// gradle clean bootrun
// gradle clean -Dtest.single=PopulateTables test
